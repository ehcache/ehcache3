/*
 * Copyright Terracotta, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.gradle.internal.jvm.Jvm

apply plugin: EhDeploy

configurations {
  tckTestClasses
}

sourceSets {
  tckTest {
    java.srcDir 'src/tck/java'
    resources.srcDir 'src/tck/resources'
    compileClasspath += sourceSets.test.compileClasspath
    runtimeClasspath += sourceSets.test.runtimeClasspath
  }
}

dependencies {
  compile project(':impl'), project(':xml')
  provided "javax.cache:cache-api:$parent.jcacheVersion"
  tckTestRuntime 'javax.cache:cache-tests:1.0.1'
  tckTestClasses('javax.cache:cache-tests:1.0.1:tests') {
    transitive = false
  }
}

javadoc {
  exclude '**/tck/**'
}

task unpackTckTests(type: Copy) {
  from {
    configurations.tckTestClasses.collect {zipTree(it)}
  }
  into sourceSets.tckTest.output.classesDir
}

task tckTest(type: Test, dependsOn: unpackTckTests) {
  executable = Jvm.current().javaExecutable
  testClassesDir = sourceSets.tckTest.output.classesDir
  classpath += sourceSets.tckTest.runtimeClasspath

  binResultsDir file("$buildDir/tck-tests-results/binary/$name")
  reports.junitXml.destination = "$buildDir/tck-tests-results"
  reports.html.destination = "$buildDir/reports/tck-tests"

  systemProperty 'java.net.preferIPv4Stack', 'true'
  systemProperty 'javax.management.builder.initial', 'org.ehcache.jsr107.internal.tck.Eh107MBeanServerBuilder'
  systemProperty 'org.jsr107.tck.management.agentId', 'Eh107MBeanServer'
  systemProperty 'javax.cache.CacheManager', 'org.ehcache.CacheManager'
  systemProperty 'javax.cache.Cache', 'org.ehcache.Cache'
  systemProperty 'javax.cache.Cache.Entry', 'org.ehcache.Cache$Entry'
  systemProperty 'javax.cache.annotation.CacheInvocationContext', 'javax.cache.annotation.impl.cdi.CdiCacheKeyInvocationContextImpl'
}

test.dependsOn tckTest


