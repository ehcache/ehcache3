/*
 * Copyright Terracotta, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'org.ehcache.build.internal-module'
  id 'org.ehcache.build.plugins.unsafe'
}

publishing.publications.withType(MavenPublication) {
  pom {
    name = 'Ehcache 3 Implementation module'
    description = 'The implementation module of Ehcache 3'
  }
}

sourceSets {
  main {
    unsafe.srcDir 'src/unsafe/java'
  }
  slowTest {
    java.srcDir 'src/slow-test/java'
    resources.srcDir 'src/slow-test/resources'
    compileClasspath += sourceSets.test.compileClasspath
    runtimeClasspath += sourceSets.test.runtimeClasspath
  }
}

task slowTest(type: Test) {
  testClassesDirs = sourceSets.slowTest.output.classesDirs
  classpath += sourceSets.slowTest.runtimeClasspath

  binResultsDir file("$buildDir/slow-tests-results/binary/$name")
  reports.junitXml.destination = file("$buildDir/slow-tests-results")
  reports.html.destination = file("$buildDir/reports/slow-tests")
}


dependencies {
  api project(':ehcache-core')
  implementation group: 'org.terracotta', name: 'offheap-store', version: parent.offheapVersion
  implementation group: 'org.ehcache', name: 'sizeof', version: parent.sizeofVersion
  implementation group: 'org.terracotta', name: 'terracotta-utilities-tools', version: parent.terracottaUtilitiesVersion
  compileOnly 'org.osgi:org.osgi.service.component.annotations:1.3.0'
  testImplementation project(':core-spi-test')
  testImplementation testFixtures(project(':ehcache-core'))
  testImplementation 'org.ow2.asm:asm:6.2'
  testImplementation 'org.ow2.asm:asm-commons:6.2'
  testImplementation ("org.terracotta:statistics:$parent.statisticVersion")
}

jar {
  from "$rootDir/NOTICE"
  bnd (
    'Export-Package': '!org.ehcache.impl.internal.*, org.ehcache.impl.*, org.ehcache.config.builders, ' +
      'org.ehcache.impl.internal.spi.loaderwriter', //ugly 107 induced internal export wart
    'Import-Package': '!sun.misc, !javax.annotation, *',
  )
}

compileUnsafe {
  //no -Werror due to unsafe
  options.compilerArgs = ['-Xlint:all']
}
