/*
 * Copyright Terracotta, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *  `
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.asciidoctor.gradle.jvm.AsciidoctorJBasePlugin
import org.asciidoctor.gradle.jvm.AsciidoctorTask

plugins {
  id 'org.asciidoctor.jvm.base'
}

asciidoctorj {
  safeMode 'UNSAFE'
  attributes 'skip-front-matter': 'true'
  fatalWarnings ~/.*/
  modules {
    diagram.version '1.5.18'
  }
}

def createCopyCssTask(def asciidocTask) {
  return tasks.register("copy${asciidocTask.name}CSS", Copy) {
    from ('css') {
      include '**'
    } into("${asciidocTask.outputDir}/css")
  }
}

tasks.withType(AsciidoctorTask) {
  group = AsciidoctorJBasePlugin.TASK_GROUP
  resources {
    from('fonts') {
      include '*'
    } into('./fonts')
  }
  dependsOn createCopyCssTask(it)
}

tasks.register('userDoc', AsciidoctorTask) {
  description = 'Generate the user documentation'
  sourceDir file('src/docs/asciidoc/user')
  outputDir file("$buildDir/asciidoc/user")
}

tasks.register('developerDoc', AsciidoctorTask) {
  description = 'Generate the developer documentation'
  sourceDir file('src/docs/asciidoc/developer')
  outputDir file("$buildDir/asciidoc/developer")
}
