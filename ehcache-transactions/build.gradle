/*
 * Copyright Terracotta, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'org.ehcache.build.public-module'
  id 'org.ehcache.build.plugins.variant'
}

publishing.publications.withType(MavenPublication) {
  pom {
    name = 'Ehcache 3 transactions module'
    description = 'The transactions module of Ehcache 3'
  }
}

variants {
  variant('module') {
    capability "org.ehcache:ehcache-transactions-provider:$version"
    capability "org.ehcache:ehcache-transactions-modules:$version"
  }
}

configurations {
  [apiElements, runtimeElements]*.outgoing {
    capability "org.ehcache:ehcache-transactions-provider:$version"
    capability "org.ehcache:ehcache-transactions:$version"
  }
}

dependencies {
  api group: 'javax.transaction', name: 'jta', version: '1.1'
  api project(':ehcache')
  implementation(group: 'org.codehaus.btm', name: 'btm', version: '2.1.4') {
    exclude group:'org.slf4j', module:'slf4j-api'
  }
  compileOnly 'org.osgi:org.osgi.service.component.annotations:1.3.0'

  testImplementation(project(':core-spi-test')) {
    exclude group:'org.ehcache.modules'
  }
  testImplementation testFixtures(project(':ehcache-xml')) {
    exclude group:'org.ehcache.modules', module:'ehcache-xml'
  }
  testImplementation "org.terracotta:statistics:$statisticVersion"

  moduleApi group: 'javax.transaction', name: 'jta', version: '1.1'
  moduleApi project(':ehcache-core')
  moduleImplementation project(':ehcache-impl')
  moduleImplementation project(':ehcache-xml')

  moduleImplementation(group: 'org.codehaus.btm', name: 'btm', version: '2.1.4') {
    exclude group:'org.slf4j', module:'slf4j-api'
  }
}

jar {
  bnd (
    'Bundle-SymbolicName': 'org.ehcache.transactions',
    'Export-Package': 'org.ehcache.transactions.xa.*',
    'Import-Package': 'bitronix.tm.*;resolution:=optional, javax.transaction.*;resolution:=optional, org.ehcache.xml.*;resolution:=optional, *',
  )
}
