plugins {
  id 'org.ehcache.build.conventions.war' apply false
  id 'org.gretty' apply false
}

subprojects {
  apply plugin: 'org.ehcache.build.conventions.war'
  apply plugin: 'org.gretty'

  gretty {
    httpPort = 8080
    contextPath = '/'
    servletContainer = 'jetty12'
  }

  dependencies {
    implementation project(':ehcache-impl')
    implementation 'jakarta.servlet:jakarta.servlet-api:6.0.0'
    runtimeOnly 'ch.qos.logback:logback-classic:1.5.20'
    runtimeOnly 'com.h2database:h2:2.4.240'
  }

  configurations.configureEach {
    resolutionStrategy {
      dependencySubstitution {
        substitute(module('org.slf4j:slf4j-api'))
          .because('org.gretty:gretty-runner-jetty declares a very old version of slf4j')
          .using(module('org.slf4j:slf4j-api:' + project.property('slf4jVersion')))
        substitute(module('commons-cli:commons-cli:1.3.1'))
          .using(module('commons-cli:commons-cli:1.4'))
      }
    }
  }

  configurations.named('grettyRunnerJetty12') {
    resolutionStrategy {
      dependencySubstitution {
        substitute(module('jakarta.annotation:jakarta.annotation-api:2.1.0'))
          .using(module('jakarta.annotation:jakarta.annotation-api:2.1.1'))
        substitute(module('jakarta.el:jakarta.el-api:5.0.0'))
          .using(module('jakarta.el:jakarta.el-api:5.0.1'))
      }
    }
  }
}
