/*
 * Copyright Terracotta, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.gradle.internal.jvm.Jvm

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
  serverLibs
}

dependencies {
  // not required by gradle but required by the IDE because 'dist' do not has any transitive dependencies
  testCompileOnly project(':clustered:client')
  testCompileOnly project(':clustered:common')
  testCompileOnly project(':impl')
  testCompileOnly project(':xml')

  testCompile project(':dist')
  testCompile project(':clustered:clustered-dist')
  testCompile project(':management')
  testCompile "org.terracotta.management.dist:mnm-nms:$parent.managementVersion"
  testCompile "org.terracotta.management.dist:mnm-nms-agent:$parent.managementVersion"
  testCompile "com.fasterxml.jackson.core:jackson-databind:2.8.0"

  testCompile (group:'org.terracotta.internal', name:'galvan-support', version: terracottaCoreVersion) {
    // galvan-support depends on junit 4.11 and version enforcement plugin won't allow that
    exclude group:'junit', module:'junit'
  }
  testCompile (group:'com.google.code.tempus-fugit', name:'tempus-fugit', version:'1.1') {
    exclude group:'junit', module:'junit'
    exclude group:'org.hamcrest', module:'hamcrest-core'
  }
  testCompile group: 'javax.cache', name: 'cache-api', version: jcacheVersion

  serverLibs ("org.terracotta.management.dist:mnm-server:$parent.managementVersion") {
    exclude group:'org.terracotta.management.dist', module:'mnm-common'
  }
}

task unzipKit(type: Copy) {
  dependsOn project(':clustered:clustered-dist').distZip
  from zipTree(project(':clustered:clustered-dist').distZip.archivePath)
  into 'build/ehcache-kit'
}

task copyServerLibs(type: Copy) {
  dependsOn unzipKit
  from project.configurations.serverLibs
  into "$unzipKit.destinationDir/${project(':clustered:clustered-dist').archivesBaseName}-$project.version-kit/server/plugins/lib"
}

compileTestJava {
  options.forkOptions.executable = Jvm.current().javacExecutable
}

test {
  dependsOn copyServerLibs
  executable = Jvm.current().javaExecutable
  // If you want to see all mutations of the voltron monitoring tree, add to JAVA_OPTS: -Dorg.terracotta.management.service.monitoring.VoltronMonitoringService.DEBUG=true
  environment 'JAVA_OPTS', '-Dcom.tc.l2.lockmanager.greedy.locks.enabled=false'
  //If this directory does not exist, tests will fail with a cryptic assert failure
  systemProperty 'kitInstallationPath', "$unzipKit.destinationDir/${project(':clustered:clustered-dist').archivesBaseName}-$project.version-kit"
  // Uncomment to include client logging in console output
  // testLogging.showStandardStreams = true
}

tasks.withType(JavaCompile) {
  options.compilerArgs += ['-Werror']
}
