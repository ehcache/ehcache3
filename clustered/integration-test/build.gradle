/*
 * Copyright Terracotta, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

configurations {
  serverLibs
}

dependencies {
  // not required by gradle but required by the IDE because 'dist' does not have any transitive dependencies
  testCompileOnly project(':clustered:client')
  testCompileOnly project(':clustered:common')
  testCompileOnly project(':impl')
  testCompileOnly project(':xml')
  testCompileOnly "org.terracotta.internal:client-runtime:$terracottaCoreVersion"
  testCompileOnly "org.terracotta:runnel:$terracottaPlatformVersion"
  testCompileOnly "org.terracotta:lease-api:$terracottaPlatformVersion"

  testImplementation project(':management')
  testImplementation "org.terracotta.management.dist:mnm-nms:$terracottaPlatformVersion"
  testImplementation "org.terracotta.management.dist:mnm-nms-agent:$terracottaPlatformVersion"
  testImplementation "com.fasterxml.jackson.core:jackson-databind:2.8.0"
  testImplementation "org.awaitility:awaitility:3.1.6"
  testRuntimeOnly project(':clustered:clustered-dist')
  testRuntimeOnly project(':dist')

  testRuntimeOnly "org.glassfish.jaxb:jaxb-runtime:$parent.jaxbVersion"

  testImplementation (group:'org.terracotta.internal', name:'galvan-support', version: terracottaCoreVersion)
  testImplementation (group:'com.google.code.tempus-fugit', name:'tempus-fugit', version:'1.1') {
    exclude group:'junit', module:'junit'
    exclude group:'org.hamcrest', module:'hamcrest-core'
  }
  testImplementation group: 'javax.cache', name: 'cache-api', version: jcacheVersion

  serverLibs ("org.terracotta.management.dist:mnm-server:$terracottaPlatformVersion") {
    exclude group:'org.terracotta.management.dist', module:'mnm-common'
  }
}

task unzipKit(type: Copy) {
  dependsOn project(':clustered:clustered-dist').distZip
  from zipTree(project(':clustered:clustered-dist').distZip.archivePath)
  into 'build/ehcache-kit'
}

task copyServerLibs(type: Copy) {
  dependsOn unzipKit
  from project.configurations.serverLibs
  into "$unzipKit.destinationDir/${project(':clustered:clustered-dist').archivesBaseName}-$project.version-kit/server/plugins/lib"
}

test {
  dependsOn copyServerLibs
  environment 'JAVA_HOME', testJava.javaHome
  if (testJava.javaVersion.isJava9Compatible()) {
    environment 'JAVA_OPTS', '--add-modules java.xml.bind'
  }
  //If this directory does not exist, tests will fail with a cryptic assert failure
  systemProperty 'kitInstallationPath', "$unzipKit.destinationDir/${project(':clustered:clustered-dist').archivesBaseName}-$project.version-kit"
  // Uncomment to include client logging in console output
  // testLogging.showStandardStreams = true
}.doFirst {
  if (testJava.javaVersion.isJava11Compatible()) throw new StopExecutionException("Clustered Tests Not Working in Java 11")
}

