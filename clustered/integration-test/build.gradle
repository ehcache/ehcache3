/*
 * Copyright Terracotta, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

dependencies {
  testCompile project(':clustered:dist')

  testCompile group:'org.terracotta', name:'galvan-support', version:'1.0.0-SNAPSHOT' changing true
}

task unzipKit(type: Copy) {
  dependsOn project(':clustered:dist').distZip
  from zipTree(project(':clustered:dist').distZip.archivePath)
  into 'build/ehcache-kit'
}

def java8 = {
  JavaVersion.current().isJava8Compatible()
}

compileTestJava {
  options.fork = true;
  options.forkOptions.executable = MavenToolchain.javaExecutable(JavaVersion.VERSION_1_8, 'javac')
}

test {
  dependsOn unzipKit
  executable = MavenToolchain.javaExecutable(JavaVersion.VERSION_1_8, 'java')
  environment 'JAVA_HOME', MavenToolchain.javaHome(JavaVersion.VERSION_1_8)
  environment 'JAVA_OPTS', '-Dcom.tc.l2.lockmanager.greedy.locks.enabled=false'
  systemProperty 'kitInstallationPath', "$unzipKit.destinationDir/ehcache-clustered-$project.version-kit/terracotta-$parent.terracottaCoreVersion"
  // Uncomment to include client logging in console output
  // testLogging.showStandardStreams = true
}

