/*
 * Copyright Terracotta, Inc.
 * Copyright IBM Corp. 2024, 2025
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  //OWASP Security Vulnerability Detection
  id 'org.owasp.dependencycheck'
  id 'com.jfrog.artifactory'
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

allprojects {
  version = findProperty('overrideVersion') ?: ehcacheVersion
}

assert JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17) : 'The Ehcache 3 build requires Java 17+ to run'

dependencyCheck {
  failBuildOnCVSS = 0
  suppressionFile = 'config/owasp-supressions.xml'
  skipConfigurations += ['checkstyle', 'spotbugs', 'xjcClasspath', 'jakartaXjcClasspath', 'commonXjcClasspath']
  skipProjects += [':docs', ':demos:00-NoCache', ':demos:01-CacheAside']
  
  formats = ['HTML', 'JSON']
  
  nvd {
    apiKey = project.hasProperty('nvdApiKey') ?  project.property('nvdApiKey') : System.getenv("NVD_API_KEY")
    validForHours = 24 // Cache NVD data for 24 hours
  }
  
  analyzers {
    // Disable analyzers that aren't needed
    assemblyEnabled = false
    ossIndexEnabled = false
  }
}
tasks.register('check') {
  dependsOn dependencyCheckAggregate
}

if (project.hasProperty('overrideVersion') && project.property('overrideVersion').toString().contains("-internal")) {
    artifactoryPublish.skip = true

    artifactory {
        contextUrl = "https://na.artifactory.swg-devops.com/artifactory" // Replace with your Artifactory URL
        publish {
            repository {
                releaseRepoKey = "hyc-webmriab-team-tc01-os-releases-maven-local"
                snapshotRepoKey = "hyc-webmriab-team-tc01-os-snapshots-maven-local" // The Artifactory repository key to publish to
                username = project.findProperty("artifactory_user") ?: System.getenv("ARTIFACTORY_DEPLOY_USERNAME")
                password = project.findProperty("artifactory_password") ?: System.getenv("ARTIFACTORY_DEPLOY_PASSWORD")
            }
            defaults {
                publications('mavenJava')
            }
            buildInfo {
                buildName = "hyc-webmriab-team-tc01/terracotta/${rootProject.name}"
                buildNumber = rootProject.findProperty("buildNumber") ?: '' + new Random(System.currentTimeMillis()).nextInt(20000)
            }
        }
    }
}

