/*
 * Copyright Terracotta, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  // This adds tasks to auto close or release nexus staging repos
  // see https://github.com/Codearte/gradle-nexus-staging-plugin/
  id 'io.codearte.nexus-staging'
  //OWASP Security Vulnerability Detection
  id 'org.owasp.dependencycheck'
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

allprojects {
  version = findProperty('overrideVersion') ?: ehcacheVersion
}

if (deployUrl.contains('nexus')) {
  //internal terracotta config, shorten url for this plugin to end at local/
  project.nexusStaging {
    serverUrl = deployUrl.replaceAll(~/local\/.*$/, "local/")
    packageGroup = 'Ehcache OS' //internal staging repository name
  }
  ext {
    deployUser = tcDeployUser
    deployPwd = tcDeployPassword
  }
} else {
  project.nexusStaging {
    packageGroup = 'org.ehcache' //Sonatype staging repository name
  }
  ext {
    deployUser = sonatypeUser
    deployPwd = sonatypePwd
  }
}

nexusStaging {
  username = project.ext.deployUser
  password = project.ext.deployPwd
  logger.debug("Nexus Staging: Using login ${username} and url ${serverUrl}")
}

tasks.named('closeAndReleaseRepository') {
  // Disable automatic promotion for added safety
  enabled = false;
}

assert (JavaVersion.current().isJava8Compatible()) : 'The Ehcache 3 build requires Java 8+ to run'

dependencyCheck {
  failBuildOnCVSS = 0
  suppressionFile = 'config/owasp-supressions.xml'
  skipConfigurations += ['checkstyle', 'spotbugs', 'xjcClasspath']
  skipProjects += [':docs', ':demos:00-NoCache', ':demos:01-CacheAside']
}
tasks.register('check') {
  dependsOn dependencyCheckAggregate
}
