/*
 * Copyright Terracotta, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import scripts.*
import org.gradle.internal.jvm.Jvm

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
  }
}

// This adds tasks to auto close or release nexus staging repos
// see https://github.com/Codearte/gradle-nexus-staging-plugin/
project.plugins.apply 'io.codearte.nexus-staging'
project.nexusStaging {
  username = project.sonatypeUser
  password = project.sonatypePwd
  packageGroup = 'org.ehcache'
}

// Disable automatic promotion for added safety
closeAndPromoteRepository.enabled = false


ext {

  baseVersion = findProperty('overrideVersion') ?: '3.2.3'

  // Third parties
  offheapVersion = '2.3.2'
  statisticVersion = '1.4.2'
  jcacheVersion = '1.0.0'
  slf4jVersion = '1.7.7'
  sizeofVersion = '0.3.0'
  ehcache2Version = '2.10.3'

  // Clustered
  terracottaPlatformVersion = '5.1.1'
  managementVersion = terracottaPlatformVersion
  terracottaApisVersion = '1.1.1'
  terracottaCoreVersion = '5.1.1'
  offheapResourceVersion = terracottaPlatformVersion
  entityApiVersion = terracottaApisVersion
  terracottaPassthroughTestingVersion = '1.1.1'
  entityTestLibVersion = terracottaPassthroughTestingVersion
  galvanVersion = '1.1.1'

  // Tools
  findbugsVersion = '3.0.1'

  utils = new Utils(baseVersion, logger)
  isReleaseVersion = !baseVersion.endsWith('SNAPSHOT')
  isCloudbees = System.getenv('JENKINS_URL')?.contains('cloudbees')
}

if (deployUrl.contains('nexus')) {
  ext {
    deployUser = tcDeployUser
    deployPwd = tcDeployPassword
  }
} else {
  ext {
    deployUser = sonatypeUser
    deployPwd = sonatypePwd
  }
}

// Java 6 build setup
def java6Error = 'Set the poperty \'java6Home\' in your $HOME/.gradle/gradle.properties pointing to a Java 6 installation'
assert (JavaVersion.current().isJava8Compatible()) : 'The Ehcache 3 build requires Java 8 to run and a configured Java 6 installation\n' + java6Error
assert hasProperty('java6Home') : 'The Ehcache 3 build requires a configured Java 6 installation\n' + java6Error
def java6HomeLocation = new File(java6Home)
def testJavaHomeLocation = java6HomeLocation

if (hasProperty('testVM')) {
  switch (testVM) {
    case '6':
      break
    case '7':
      assert hasProperty('java7Home') : 'Set the poperty \'java7Home\' in your $HOME/.gradle/gradle.properties pointing to a Java 7 installation'
      testJavaHomeLocation = new File(java7Home)
      break
    case '8':
      testJavaHomeLocation = Jvm.current().javaHome
      break
    default:
      throw new AssertionError("Unrecognized 'testVM' value $testVM - Accepted values are 7 or 8")
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'checkstyle'
  apply plugin: 'findbugs'
  apply plugin: 'jacoco'

  group = 'org.ehcache.modules'
  version = baseVersion

  archivesBaseName = "ehcache-${project.name}"

  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  repositories {
    if (project.hasProperty('mvnlocal')) {
      mavenLocal()
    }
    mavenCentral()
    maven { url "http://repo.terracotta.org/maven2" }
  }

  sourceSets {
    slowTest {
      java.srcDir 'src/slow-test/java'
      resources.srcDir 'src/slow-test/resources'
      compileClasspath += sourceSets.test.compileClasspath
      runtimeClasspath += sourceSets.test.runtimeClasspath
    }
  }

  dependencies {
    compileOnly "com.google.code.findbugs:annotations:$parent.findbugsVersion"
    testCompileOnly "com.google.code.findbugs:annotations:$parent.findbugsVersion"
    testCompile 'junit:junit:4.12', 'org.assertj:assertj-core:1.7.1', 'org.hamcrest:hamcrest-library:1.3'
    testCompile('org.mockito:mockito-core:1.9.5') {
      exclude group:'org.hamcrest', module:'hamcrest-core'
    }
    testRuntime "org.slf4j:slf4j-simple:$parent.slf4jVersion"
  }

  jar {
    utils.fillManifest(manifest,"ehcache-${project.name}")
    from "$rootDir/LICENSE"
  }

  test {
    maxHeapSize = "1408m"
    systemProperty 'java.awt.headless', 'true'
    if (parent.isCloudbees) {
      systemProperty 'disable.concurrent.tests', 'true'
    }
  }

  task slowTest(type: Test) {
    testClassesDir = sourceSets.slowTest.output.classesDir
    classpath += sourceSets.slowTest.runtimeClasspath

    binResultsDir file("$buildDir/slow-tests-results/binary/$name")
    reports.junitXml.destination = "$buildDir/slow-tests-results"
    reports.html.destination = "$buildDir/reports/slow-tests"
  }

  task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allJava
    classifier = 'sources'
  }

  javadoc {
    title "$project.archivesBaseName $project.version API"
    exclude '**/internal/**'
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
  }

  artifacts {
    archives jar

    archives javadocJar
    archives sourceJar
  }

  checkstyle {
    configFile = file("$rootDir/config/checkstyle.xml")
    configProperties = ['projectDir':projectDir, 'rootDir':rootDir]
    toolVersion = '5.9'
  }

  findbugs {
    ignoreFailures = false
    sourceSets = [sourceSets.main]
    findbugs.toolVersion = parent.findbugsVersion
  }

  jacoco {
    toolVersion = '0.7.1.201405082137'
  }

  jacocoTestReport {
    reports {
      xml.enabled false
      csv.enabled false
    }
  }

  tasks.withType(AbstractCompile) {
    options.with {
      fork = true
      forkOptions.executable = utils.executables(java6HomeLocation).javac
    }
  }
  tasks.withType(Test) {
    executable = utils.executables(testJavaHomeLocation).java
  }
  tasks.withType(JavaExec) {
    executable = utils.executables(testJavaHomeLocation).java
  }
  tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
  }

  configurations.all {
    resolutionStrategy {
      failOnVersionConflict()
    }
  }
}

allprojects {
  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-Xlint:unchecked']
  }
  tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
  }
}
