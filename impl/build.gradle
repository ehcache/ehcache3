/*
 * Copyright Terracotta, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: EhDeploy
apply plugin: 'osgi'
apply plugin: 'osgi-ds'

dependencies {
  api project(':core')
  implementation group: 'org.terracotta', name: 'offheap-store', version: parent.offheapVersion
  implementation (group: 'org.ehcache', name: 'sizeof', version: parent.sizeofVersion) {
    exclude group:'org.slf4j', module:'slf4j-api'
  }
  implementation ("org.terracotta:statistics:$parent.statisticVersion")
  compileOnly 'org.osgi:org.osgi.service.component.annotations:1.3.0'
  testImplementation project(':core-spi-test')
  testImplementation 'org.ow2.asm:asm:6.2'
  testImplementation 'org.ow2.asm:asm-commons:6.2'
}

jar {
  from "$rootDir/NOTICE"
  manifest {
    instruction 'Export-Package',
      'org.ehcache.impl.internal.concurrent',
      'org.ehcache.impl.internal.events',
      'org.ehcache.impl.internal.spi.loaderwriter',
      'org.ehcache.impl.internal.spi.serialization',
      'org.ehcache.impl.internal.store.loaderwriter',
      'org.ehcache.impl.internal.util',

      '!org.ehcache.impl.internal.*',
      'org.ehcache.impl.*', 'org.ehcache.config.builders'
    instruction 'Import-Package', '!sun.misc', '*'
    instruction 'Service-Component', 'OSGI-INF/*.xml'
  }
}

compileJava {
  //no -Werror due to unsafe
  options.compilerArgs = ['-Xlint:all']
}
