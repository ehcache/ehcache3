/*
 * Copyright Terracotta, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'org.ehcache.build.deploy'
  id 'biz.aQute.bnd.builder'
  id 'org.jayware.osgi-ds'
}

sourceSets {
  unsafe {
    java {
      srcDir 'src/unsafe/java'
    }
  }
}

dependencies {
  api project(':core')
  implementation group: 'org.terracotta', name: 'offheap-store', version: parent.offheapVersion
  implementation (group: 'org.ehcache', name: 'sizeof', version: parent.sizeofVersion) {
    exclude group:'org.slf4j', module:'slf4j-api'
  }
  implementation group: 'org.terracotta', name: 'terracotta-utilities-tools', version: parent.terracottaUtilitiesVersion
  compileOnly 'org.osgi:org.osgi.service.component.annotations:1.3.0'
  testImplementation project(':core-spi-test')
  testImplementation 'org.ow2.asm:asm:6.2'
  testImplementation 'org.ow2.asm:asm-commons:6.2'
  testImplementation ("org.terracotta:statistics:$parent.statisticVersion")

  unsafeImplementation project(':api')
  unsafeCompileOnly "com.github.spotbugs:spotbugs-annotations:${project.spotbugs.toolVersion}"
  api files(sourceSets.unsafe.output.classesDirs) {
    builtBy compileUnsafeJava
  }
}

jar {
  from sourceSets.unsafe.output
  from "$rootDir/NOTICE"
  bnd (
    'Export-Package': '!org.ehcache.impl.internal.*, org.ehcache.impl.*, org.ehcache.config.builders, ' +
      'org.ehcache.impl.internal.spi.loaderwriter', //ugly 107 induced internal export wart
    'Import-Package': '!sun.misc, !javax.annotation, *',
  )
}

sourceJar {
  from sourceSets.unsafe.allSource
}

compileUnsafeJava {
  //no -Werror due to unsafe
  options.compilerArgs = ['-Xlint:all']
}
